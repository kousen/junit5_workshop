plugins {
    id 'java'
    id("com.diffplug.spotless") version "7.1.0"
    alias(libs.plugins.versions)
    alias(libs.plugins.version.catalog.update)
}

group 'com.kousenit'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    // JUnit BOM for consistent versions
    testImplementation platform("org.junit:junit-bom:5.13.4")
    
    implementation libs.gson
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation libs.bundles.mockito
    testImplementation libs.assertj
    testImplementation libs.jqwik
    testImplementation libs.commons.validator
    testImplementation libs.commons.collections
}

tasks.named('test', Test) {
    useJUnitPlatform {
        excludeTags 'slow'
    }
    maxParallelForks = Runtime.runtime.availableProcessors() / 2 + 1
    
    // Disable class data sharing
    jvmArgs '-Xshare:off'
}

